import me.champeau.jmh.JmhBytecodeGeneratorTask
import org.gradle.internal.os.OperatingSystem
import org.ajoberstar.grgit.Grgit
import java.time.Duration
import jdk.incubator.vector.ByteVector

plugins {
    id 'java'
    id 'scala'
    id 'me.champeau.jmh' version '0.7.1'
    id 'org.ajoberstar.grgit' version '5.2.0'
    id 'pl.allegro.tech.build.axion-release' version '1.15.4'
    id 'io.github.gradle-nexus.publish-plugin' version '1.3.0'
    id 'maven-publish'
    id 'signing'
}

group = 'org.simdjson'
version = scmVersion.version

scmVersion {
    versionCreator('versionWithBranch')
    tag {
        prefix = ''
    }
}

repositories {
    mavenCentral()
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(20)
    }
    withJavadocJar()
    withSourcesJar()
}

ext {
    junitVersion = '5.9.1'
}

dependencies {
    jmhImplementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.15.2'
    jmhImplementation group: 'com.alibaba.fastjson2', name: 'fastjson2', version: '2.0.35'
    jmhImplementation group: 'com.jsoniter', name: 'jsoniter', version: '0.9.23'
    jmhImplementation group: 'com.github.plokhotnyuk.jsoniter-scala', name: 'jsoniter-scala-core_2.13', version: '2.23.2'
    compileOnly group: 'com.github.plokhotnyuk.jsoniter-scala', name: 'jsoniter-scala-macros_2.13', version: '2.23.2'

    testImplementation group: 'org.assertj', name: 'assertj-core', version: '3.24.2'
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: junitVersion
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: junitVersion
    testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: junitVersion
}

tasks.register('downloadTestData') {
    doFirst {
        def testDataDir = new File("${project.projectDir.getAbsolutePath()}/testdata")
        if (!testDataDir.exists()) {
            testDataDir.mkdir()
        }
        def numbersTestDataDir = new File("${testDataDir}/parse-number-fxx-test-data")
        if (!numbersTestDataDir.exists()) {
            def grgit = Grgit.clone(dir: numbersTestDataDir, uri: 'https://github.com/nigeltao/parse-number-fxx-test-data.git')
            grgit.close()
        }
    }
}

tasks.register('testPreferred', Test) {
    dependsOn downloadTestData
    useJUnitPlatform()
    jvmArgs += [
            '--add-modules', 'jdk.incubator.vector',
            '-Xmx2g'
    ]
}

tasks.register('test256', Test) {
    dependsOn downloadTestData
    useJUnitPlatform()
    onlyIf() {
        ByteVector.SPECIES_PREFERRED == ByteVector.SPECIES_512 
    }
    jvmArgs += [
            '--add-modules', 'jdk.incubator.vector',
            '-Xmx2g',
            '-XX:UseAVX=2'
    ]
}

test {
    dependsOn 'testPreferred'
    dependsOn 'test256'
}

tasks.withType(JmhBytecodeGeneratorTask).configureEach {
    jvmArgs.set(["--add-modules=jdk.incubator.vector"])
}

tasks.withType(JavaCompile).configureEach {
    options.compilerArgs.add("--add-modules=jdk.incubator.vector")
}

tasks.compileJmhScala.classpath = sourceSets.main.compileClasspath

tasks.compileJmhJava.classpath += files(sourceSets.jmh.scala.classesDirectory)

compileTestJava {
    options.compilerArgs += [
            '--add-modules', 'jdk.incubator.vector'
    ]
}

javadoc.options {
    addStringOption('-add-modules', 'jdk.incubator.vector')
}

jmh {
    fork = 1
    warmupIterations = 3
    iterations = 5
    jvmArgsPrepend = [
            '--add-modules=jdk.incubator.vector'
    ]
    if (getBooleanProperty('jmh.profilersEnabled', false)) {
        if (OperatingSystem.current().isLinux()) {
            profilers = [
                    'perf',
                    'perfasm:intelSyntax=true',
                    'async:verbose=true;output=flamegraph;event=cpu;dir=./profilers/async;libPath=' + getAsyncProfilerLibPath('LD_LIBRARY_PATH')
            ]
        } else if (OperatingSystem.current().isMacOsX()) {
            profilers = [
                    'async:verbose=true;output=flamegraph;event=cpu;dir=./profilers/async;libPath=' + getAsyncProfilerLibPath('DYLD_LIBRARY_PATH')
            ]
        }
    }
    if (project.hasProperty('jmh.includes')) {
        includes = [project.findProperty('jmh.includes')]
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from(components.java)

            pom {
                name = project.name
                description = 'A Java version of simdjson, a high-performance JSON parser utilizing SIMD instructions.'
                url = 'https://github.com/simdjson/simdjson-java'
                issueManagement {
                    system = 'GitHub Issue Tracking'
                    url = 'https://github.com/simdjson/simdjson-java/issues'
                }
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'piotrrzysko'
                        name = 'Piotr RÅ¼ysko'
                        email = 'piotr.rzysko@gmail.com'
                    }
                }
                scm {
                    url = 'https://github.com/simdjson/simdjson-java'
                    connection = 'scm:git@github.com:simdjson/simdjson-java.git'
                    developerConnection = 'scm:git@github.com:simdjson/simdjson-java.git'
                }
            }
        }
    }
}

if (System.getenv('GPG_KEY_ID')) {
    signing {
        useInMemoryPgpKeys(
                System.getenv('GPG_KEY_ID'),
                System.getenv('GPG_PRIVATE_KEY'),
                System.getenv('GPG_PRIVATE_KEY_PASSWORD')
        )
        sign publishing.publications.mavenJava
    }
}

nexusPublishing {
    repositories {
        sonatype {
            nexusUrl = uri('https://s01.oss.sonatype.org/service/local/')
            snapshotRepositoryUrl = uri('https://s01.oss.sonatype.org/content/repositories/snapshots/')
            stagingProfileId = '3c0bbfe420699e'
            username = System.getenv('SONATYPE_USERNAME')
            password = System.getenv('SONATYPE_PASSWORD')
        }
    }
    connectTimeout = Duration.ofMinutes(3)
    clientTimeout = Duration.ofMinutes(3)
}

def getBooleanProperty(String name, boolean defaultValue) {
    Boolean.valueOf((project.findProperty(name) ?: defaultValue) as String)
}

static def getAsyncProfilerLibPath(String envVarName) {
    System.getenv(envVarName) ?: System.getProperty('java.library.path')
}
